<?xml version="1.0" encoding="UTF-8"?>

<!-- 
Group type
XPath - /xs:schema/xs:complexType[@name="groupType"]
-->
<chunk xmlns="https://www.phon.ca/ns/chunk">
    
    <!-- Parser -->
    <buffer name="TalkBank2AST.g">
        <![CDATA[
g	
	:	G_START gele+ gchoice* G_END 
	->	^(G_START gele+ gchoice*)
	;
	
gele
	:	w
	|	g
	|	pg
	|	sg
	|	quotation
	|	quotation2
	|	pause
	|	internal_media
	|	freecode
	|	e
	|	s
	|	tagmarker
	|	long_feature
	|	nonvocal
	|	overlap_point
	|	underline
	|	italic
	;
	
gchoice
	:	k
	|	error
	|	r
	|	duration
	|	ga
	|	overlap
	;
]]>
    </buffer>
    
    <!-- Tree Walker -->
    <buffer name="AST2Phon.g">
        <![CDATA[
g returns [String val,  Integer phoRepCount, Map<Integer,IPATranscript> targetReps, Map<Integer, IPATranscript> actReps, Map<Integer, PhoneMap> phoneMaps, Map<String, Map<Integer, String>> tierMaps]
scope {
	String buffer;
	
	// when g has <pg> children store the
 	// phonetic rep objects generated in the
 	// pho rule
 	Map<Integer, IPATranscript> tReps;
 	Map<Integer, IPATranscript> aReps;
	Map<Integer, PhoneMap> pMaps;
	Map<String, Map<Integer, String>> trMaps;
 	
 	int pgCount;
}
@init {
	$g::buffer = new String();
	
	$g::tReps = new HashMap<Integer, IPATranscript>();
	$g::aReps = new HashMap<Integer, IPATranscript>();
	$g::pMaps = new HashMap<Integer, PhoneMap>();
	$g::trMaps = new HashMap<String, Map<Integer, String>>();
	
	$g::pgCount = 0;
}
@after {
	$val = $g::buffer;
	
	$targetReps = $g::tReps;
	$actReps = $g::aReps;
	$phoneMaps = $g::pMaps;
	$tierMaps = $g::trMaps;
	
	$phoRepCount = $g::pgCount;
}
	:	^(G_START gele+ gchoice*)
	;
	
gele
	:	w 
	{	
		$g::buffer += ($g::buffer.length() > 0 ? " " : "") + $w.val;	
	}
	|	nestedg=g
	{
		$g::buffer += ($g::buffer.length() > 0 ? " " : "") +
			"{" + $nestedg.val + "}"; 	
		
		Integer[] targetKeys = $nestedg.targetReps.keySet().toArray(new Integer[0]);
		for(Integer targetKey:targetKeys) {
			int newKey = targetKey + $g::pgCount;
			$g::tReps.put(newKey, $nestedg.targetReps.get(targetKey));
		}
		
		Integer[] actualKeys = $nestedg.actReps.keySet().toArray(new Integer[0]);
		for(Integer actualKey:actualKeys) {
			int newKey = actualKey + $g::pgCount;
			$g::aReps.put(newKey, $nestedg.actReps.get(actualKey));
		}
		
		Integer[] pMapKeys = $nestedg.phoneMaps.keySet().toArray(new Integer[0]);
		for(Integer pmapKey:pMapKeys) {
			int newKey = pmapKey + $g::pgCount;
			$g::pMaps.put(newKey, $nestedg.phoneMaps.get(pmapKey));
		}
		
		for(String tierName:$nestedg.tierMaps.keySet()) {
			Map<Integer, String> trMap = $nestedg.tierMaps.get(tierName);
			if(trMap == null) trMap = new HashMap<>();
			
			Integer[] trMapKeys = trMap.keySet().toArray(new Integer[0]);
			for(Integer trMapKey:trMapKeys) {
				int newKey = trMapKey + $g::pgCount;
				
				Map<Integer, String> newTrMap = $g::trMaps.get(tierName);
				if(newTrMap == null) {
					newTrMap = new LinkedHashMap<>();
					$g::trMaps.put(tierName, newTrMap);
				}
				newTrMap.put(newKey, trMap.get(trMapKey));
			}
		}

		$g::pgCount += $nestedg.phoRepCount;
	}
	|	pg
	{
	 	// enclose the pg data in [] so that we know to break up
	 	// this into proper word groups in phon later.
		$g::buffer += ($g::buffer.length() > 0 ? " " : "") + "[" + $pg.val + "]";
	}
	|	sg
	{
		// TODO sg
		LOGGER.warning("sg not supported");
	}
	|	quotation
	{
		// TODO quotation
		LOGGER.warning("quotation not supported");
	}
	|	quotation2
	{
		// TODO quotation2
		LOGGER.warning("quotation2 not supported");
	}
	|	pause
	{
		$g::buffer += ($g::buffer.length() > 0 ? " " : "") + $pause.val;
	}
	|	internal_media
	{
		$g::buffer += ($g::buffer.length() > 0 ? " " : "") + $internal_media.val;
	}
	|	freecode
	|	e
	{
		$g::buffer += ($g::buffer.length() > 0 ? " " : "") +
			$e.val;
	}
	|	s
	{
		$g::buffer += ($g::buffer.length() > 0 ? " " : "") +
			$s.val;
	}
	|	tagmarker
	{
		$g::buffer += ($g::buffer.length() > 0 ? " " : "") +
			"(" + $tagmarker.val + ")";
	}
	|	long_feature
	{
		$g::buffer += ($g::buffer.length() > 0 ? " " : "") +
			"(" + $long_feature.val + ")";
	}
	|	nonvocal
	{
		$g::buffer += ($g::buffer.length() > 0 ? " " : "") +
			"(" + $nonvocal.val + ")";
	}
	|	overlap_point
	{
		$g::buffer += ($g::buffer.length() > 0 ? " " : "") +
			"(" + $overlap_point.val + ")";
	}
	|	underline
	{
		$g::buffer += ($g::buffer.length() > 0 ? " " : "") +
			$underline.val;
	}
	|	italic
	{
		 $g::buffer += ($g::buffer.length() > 0 ? " " : "") +
			"(" + $italic.val + ")";
	}
	;
	
gchoice
	: 	k
	{
		$g::buffer += ($g::buffer.length() > 0 ? " " : "") + 
			"(" + $k.val + ")";
	}
	|	error
	{
		$g::buffer += ($g::buffer.length() > 0 ? " " : "") +
			$error.val;
	}
	| 	r 
	{	
		$g::buffer += ($g::buffer.length() > 0 ? " " : "") + 
			"(x" + $r.val + ")";	
	}
	|	duration
	{
		java.text.NumberFormat nf = java.text.NumberFormat.getInstance();
		nf.setMaximumFractionDigits(3);
		$g::buffer += ($g::buffer.length() > 0 ? " " : "") +
			String.format("(# \%ss)", nf.format($duration.val));
	}
	|	ga
	{
		$g::buffer += ($g::buffer.length() > 0 ? " " : "") +
			$ga.val;
	}
	|	overlap
	{
		$g::buffer += ($g::buffer.length() > 0 ? " " : "") +
			"(" + $overlap.val + ")";
	}
	;
]]>
    </buffer>
	
	<buffer name="AST2TalkBank.g">
		<![CDATA[
g	
	:	^(G_START (contentlist+=gele)+ (choicelist+=gchoice)*)
	->	template( content={$contentlist}, choices={$choicelist} )
	<<\<g\><content; separator=""><if(choices)><choices; separator=""><endif>\</g\> >>
	;
	
gele
	:	w 
	->	template( v={$w.st} )
		"<v>"
	|	g
	->	template( v={$g.st} )
		"<v>"
	|	pg
	->	template( v={$pg.st} )
		"<v>"
	|	sg
	->	template( v={$sg.st} )
		"<v>"
	|	quotation
	->	template( v={$quotation.st} )
		"<v>"
	|	quotation2
	->	template( v={$quotation2.st} )
		"<v>"
	|	pause
	->	template( v={$pause.st} )
		"<v>"
	|	internal_media
	->	template( v={$internal_media.st} )
		"<v>"
	|	freecode
	->	template( v={$freecode.st} )
		"<v>"
	|	e
	->	template( v={$e.st} )
		"<v>"
	|	s
	->	template( v={$s.st} )
		"<v>"
	|	tagmarker
	->  template( v={$tagmarker.st} )
		"<v>"
	|	long_feature
	->	template( v={$long_feature.st} )
		"<v>"
	|	nonvocal
	->	template( v={$nonvocal.st} )
		"<v>"
	|	overlap_point
	->	template( v={$overlap_point.st} )
		"<v>"
	|	underline
	->	template( v={$underline.st} )
		"<v>"
	|	italic
	->	template( v={$italic.st} )
		"<v>"
	;
	
gchoice
	:	k
	->	template( v={$k.st} )
		"<v>"
	|	error
	->	template( v={$error.st} )
		"<v>"
	|	r 
	->	template( v={$r.st} )
		"<v>"
	|	duration
	->	template( v={$duration.st} )
		"<v>"
	|	ga
	->	template( v={$ga.st} )
		"<v>"
	|	overlap
	->	template( v={$overlap.st} )
		"<v>"
	;
]]>
	</buffer>
    
    <!-- Includes -->
	<include>w.xml</include>
	<include>pg.xml</include>
	<include>sg.xml</include>
    <include>quotation.xml</include>
	<include>quotation2.xml</include>
	<include>pause.xml</include>
	<include>internalmedia.xml</include>
	<include>freecode.xml</include>
	<include>e.xml</include>
	<include>s.xml</include>
	<include>tagmarker.xml</include>
	<include>longfeature.xml</include>
	<include>nonvocal.xml</include>
	<include>overlappoint.xml</include>
	<include>underline.xml</include>
	<include>italic.xml</include>
	<include>k.xml</include>
	<include>error.xml</include>
	<include>r.xml</include>
	<include>duration.xml</include>
	<include>ga.xml</include>
	<include>overlap.xml</include>
	
</chunk>
