<?xml version="1.0" encoding="UTF-8"?>

<!-- 
Utterance
Schema - http://www.talkbank.org/software/talkbank.xsd
XPath -	/xs:schema/xs:complexType[@name="speakerUtteranceType"]
-->
<chunk xmlns="https://www.phon.ca/ns/chunk">
    
    <!-- Parser -->
    <buffer name="TalkBank2AST.g">
        <![CDATA[
u
	:	U_START u_attrs* linker* ( uele )* t postcode* media? uendele* U_END 
	->	^(U_START u_attrs* linker* uele* t postcode* media? uendele*)
	;
	catch [RecognitionException re] {
		consumeUntil(input, U_END);
		input.consume(); // consume U_END
	}
	
u_attrs
	:	U_ATTR_WHO
	|	U_ATTR_LANG
	;
	
uele
	:	ugrp
	|	uannotation
	;
	
ugrp
	:	w 
	|	pg 
	|	g
	|	sg
	;
	
uannotation
	: 	blob
	|	quotation
	|	quotation2
	|	pause 
	|	internal_media
	|	freecode
	|	e
	|	s
	|	tagmarker
	|	overlap_point
	|	underline
	|	italic
	|	long_feature
	|	nonvocal
	;
	
uendele
	:	k
	|	error
	|	r
	|	a
	;
]]>
    </buffer>
    
    <!-- Tree Walker -->
    <buffer name="AST2Phon.g">
        <![CDATA[
u returns [Record record]
scope {
	Record r;
	
	// when adding annotations, sometimes we need to create
	// the first word-group.  This flag tells the next ugrp
	// element to use the last group instead of making a new one
	boolean useLastGroup;
	
	String who;
	String recordLang;
	
	List<String> links;
}
@init {
	$u::r = sessionFactory.createRecord();
	$u::useLastGroup = false;
	$u::who = null;
	$u::recordLang = null;
	$u::links = new ArrayList<>();
}
@after {
	$u.record = $u::r;
	++recordIndex;
}
	:	^(U_START u_attrs* (linker{$u::links.add($linker.val);})* uele* t postcode* seg=media? uendele*)
	{
		// setup speaker
		if($u::who != null) 
		{
			// try to find the speaker
			String partId = $u::who;
			
			// by id first
			for(Participant p:session.getParticipants()) {
				if(p.getId().equals(partId)) {
					$u::r.setSpeaker(p);
					break;
				}
			}
			
		}
		
		if($u::links.size() > 0) {
			OrthographyBuilder builder = new OrthographyBuilder();
			$u::links.forEach( l -> builder.append(l) );
			builder.append($u::r.getOrthography().getGroup(0));
			$u::r.getOrthography().setGroup(0, builder.toOrthography());
		}
		
		// set media if avail
		if($seg.val != null)
		{
			$u::r.getSegment().setGroup(0, $seg.val);
		}
		
		if(isSyllabifyAndAlign()) {
			Syllabifier syllabifier = getSyllabifier();
			if($u::recordLang != null) {
				// make sure the uttlang tier exists in session
				TierDescription langTierDesc = ensureTierExists("uttlang", false);
				
				Tier<String> langTier = $u::r.getTier(langTierDesc.getName(), String.class);
				if(langTier == null) {
					langTier = sessionFactory.createTier(langTierDesc.getName(), String.class, false);
					$u::r.putTier(langTier);
				}
				langTier.setGroup(0, $u::recordLang);
				
				try {
					Language lang = Language.parseLanguage($u::recordLang);
					SyllabifierLibrary library = SyllabifierLibrary.getInstance();
					Syllabifier s = library.getSyllabifierForLanguage(lang);
					if(s != null) syllabifier = s;
				} catch (IllegalArgumentException e) {
					LOGGER.log(Level.WARNING, e.getLocalizedMessage(), e);
				}
			}
			
			// syllabify/align IPA as necessary
			for(int gIdx = 0; gIdx < $u::r.numberOfGroups(); gIdx++) {
				Group g = $u::r.getGroup(gIdx);
				
				IPATranscript model = g.getIPATarget();
				boolean isSyllabifyModel = 
						model != null && model.length() > 0;
				if(isSyllabifyModel) {
					syllabifier.syllabify(model.toList());
				}
				
				IPATranscript actual = g.getIPAActual();
				boolean isSyllabifyActual = 
						actual != null && actual.length() > 0;
				if(isSyllabifyActual) {
					syllabifier.syllabify(actual.toList());
				}
				
				PhoneMap pm = g.getPhoneAlignment();
				if(pm == null || pm.getAlignmentLength() == 0) {
					pm = (new PhoneAligner()).calculatePhoneAlignment(model, actual);
					g.setPhoneAlignment(pm);
				}
			}
		}
		
		// add comments if necessary
		if(nextRecordComments.size() > 0) {
			nextRecordComments.forEach( c -> $u::r.addComment(c) );
			nextRecordComments.clear();
		}
		
		$u.record = $u::r;
	}
	;
	
u_attrs
	:	U_ATTR_WHO
	{
		$u::who = $U_ATTR_WHO.text;
	}
	|	U_ATTR_LANG
	{
		$u::recordLang = $U_ATTR_LANG.text;
	}
	;

uele
	:	ugrp
	|	uannotation
	;

/**
 * Each ugrp object creates a new group in Phon.
 */
 ugrp returns [Group group]
 scope {
 	Group g;
 }
 @init {
 	if($u::useLastGroup) {
 		$ugrp::g = $u::r.getGroup($u::r.numberOfGroups()-1);
 		
 		// reset flag
 		$u::useLastGroup = false;
 	} else {
 		$ugrp::g = $u::r.addGroup();
 	}
 }
@after {
 	$ugrp.group = $ugrp::g;
}
	:	w 
	{	
		OrthographyBuilder builder = new OrthographyBuilder();
		builder.append($ugrp::g.getOrthography()).append($w.val);
		$ugrp::g.setOrthography(builder.toOrthography());
	}
	| 	pg 
	{	
		OrthographyBuilder builder = new OrthographyBuilder();
		builder.append($ugrp::g.getOrthography()).append($pg.val);
		$ugrp::g.setOrthography(builder.toOrthography());
	}
	| 	g
	{	
		// we may need to break data up into more words
		// check to see if we have sub-[] enclosures
		String gData = $g.val;
		if(gData.contains("[") || gData.contains("]")) {
			// break up data
			List<String> grps = new ArrayList<String>();
			String currentGrp = "";
			for(int i = 0; i < gData.length(); i++) {
				char c = gData.charAt(i);
				
				if(c == '[') {
	                if(currentGrp.trim().length() > 0) {
	                    grps.add(currentGrp.trim());
	                }
	                currentGrp = "";
	            }
	            currentGrp += c;
	            if(c == ']') {
	                if(currentGrp.trim().length() > 0) {
	                    grps.add(currentGrp.trim());
	                }
	                currentGrp = "";
	            }
					
			}
			if(currentGrp.trim().length() > 0) {
				grps.add(currentGrp.trim());
			}
			// place only an openeing '{' in our first group
			// this tells the tree builder   to create this group surrounding the 
			// next <pg> groups
			$ugrp::g.setOrthography(
				(new OrthographyBuilder()).append("{").toOrthography());
			
			// if we have sub-pg groups.  The new words should have been created.
			int phoRepIndex = 1;
			for(String g:grps) {
				Group nextGroup = $u::r.addGroup();
				
				if(g.startsWith("[")) {
					nextGroup.setOrthography(
						(new OrthographyBuilder()).append(g.substring(1, g.length()-1))
							.toOrthography());
					
					// add phonetic reps generated by pho rule
					IPATranscript tRep = $g.targetReps.get(phoRepIndex);
					if(tRep == null) tRep = new IPATranscript();
					nextGroup.setIPATarget(tRep);

					IPATranscript aRep = $g.actReps.get(phoRepIndex);
					if(aRep == null) aRep = new IPATranscript();
					nextGroup.setIPAActual(aRep);
					
					PhoneMap pm = $g.phoneMaps.get(phoRepIndex);
					if(pm == null) 
						pm = (new PhoneAligner()).calculatePhoneMap(tRep, aRep);
					nextGroup.setPhoneAlignment(pm);
					
					for(String tierName:$g.tierMaps.keySet()) {
						Tier<String> tier = $u::r.getTier(tierName, String.class);
						if(tier == null) {
							tier = sessionFactory.createTier(tierName, String.class, true);
							for(int i = 0; i < $u::r.numberOfGroups(); i++) tier.addGroup();
							$u::r.putTier(tier);
						}
						String grpVal = $g.tierMaps.get(tierName).get(phoRepIndex);
						if(grpVal == null) grpVal = "";
						nextGroup.setTier(tierName, String.class, grpVal);
					}
						
					++phoRepIndex;
				} else {
					nextGroup.setOrthography(
						(new OrthographyBuilder()).append(g).toOrthography());
				}
			}
			
			// finish 'global' group
			Group endGrp = $u::r.addGroup();
			endGrp.setOrthography(
				(new OrthographyBuilder()).append("}").toOrthography());
		} else {
			$ugrp::g.setOrthography(
				(new OrthographyBuilder()).append($ugrp::g.getOrthography())
					.append("{").append($g.val).append("}")
					.toOrthography());
					
			// IPA Transcriptions
			IPATranscript ipaT = $g.targetReps.get(0);
			if(ipaT != null) $ugrp::g.setIPATarget(ipaT);
			
			IPATranscript ipaA = $g.actReps.get(0);
			if(ipaA != null) $ugrp::g.setIPAActual(ipaA);
			
			PhoneMap pm = $g.phoneMaps.get(0);
			if(pm != null) $ugrp::g.setPhoneAlignment(pm);
			
			// other tiers
			for(String tierName:$g.tierMaps.keySet()) {
				Tier<String> tier = $u::r.getTier(tierName, String.class);
				if(tier == null) {
					tier = sessionFactory.createTier(tierName, String.class, true);
					for(int i = 0; i < $u::r.numberOfGroups(); i++) tier.addGroup();
					$u::r.putTier(tier);
				}
				String grpVal = $g.tierMaps.get(tierName).get(0);
				if(grpVal == null) grpVal = "";
				$ugrp::g.setTier(tierName, String.class, grpVal);
			}
		}
	}
	|	sg
	{
		// TODO support sign groups
		LOGGER.warning("Sign groups are not supported");
	}
	;
	
uannotation
	:	blob
	{
		Group lastGroup = 
			($u::r.numberOfGroups() > 0 ? $u::r.getGroup($u::r.numberOfGroups()-1) : $u::r.addGroup());
		lastGroup.setOrthography(
			(new OrthographyBuilder()).append(lastGroup.getOrthography())
				.append($blob.val).toOrthography());
	}
	|	quotation
	{
		// TODO quotation
		LOGGER.warning("quotation not supported");
	}
	|	quotation2
	{
		// TODO quotation2
		LOGGER.warning("quotation2 not supported");
	}
	|	pause
	{
		// add pause to last group as a comment
		Group lastGroup = 
			($u::r.numberOfGroups() > 0 ? $u::r.getGroup($u::r.numberOfGroups()-1) : $u::r.addGroup());
		lastGroup.setOrthography(
			(new OrthographyBuilder()).append(lastGroup.getOrthography())
				.append($pause.val).toOrthography());
	}
	|	internal_media
	{
		Group lastGroup = 
			($u::r.numberOfGroups() > 0 ? $u::r.getGroup($u::r.numberOfGroups()-1) : $u::r.addGroup());
		lastGroup.setOrthography(
			(new OrthographyBuilder()).append(lastGroup.getOrthography())
				.append($internal_media.val).toOrthography());
	}
	|	freecode
	{
		Group lastGroup = 
			($u::r.numberOfGroups() > 0 ? $u::r.getGroup($u::r.numberOfGroups()-1) : $u::r.addGroup());
		lastGroup.setOrthography(
			(new OrthographyBuilder()).append(lastGroup.getOrthography())
				.append($freecode.val).toOrthography());
	}
	|	e
	{
		// add event to last group
		// add pause to last group as a comment
		Group lastGroup = 
			($u::r.numberOfGroups() > 0 ? $u::r.getGroup($u::r.numberOfGroups()-1) : $u::r.addGroup());
		lastGroup.setOrthography(
			(new OrthographyBuilder()).append(lastGroup.getOrthography())
				.append($e.val).toOrthography());
	}
	|	s
	{
		// add separator to last group
		Group lastGroup = 
			($u::r.numberOfGroups() > 0 ? $u::r.getGroup($u::r.numberOfGroups()-1) : $u::r.addGroup());
		lastGroup.setOrthography(
			(new OrthographyBuilder()).append(lastGroup.getOrthography())
				.append($s.val).toOrthography());
	}
	|	tagmarker
	{
		Group lastGroup = 
			($u::r.numberOfGroups() > 0 ? $u::r.getGroup($u::r.numberOfGroups()-1) : $u::r.addGroup());
		lastGroup.setOrthography(
			(new OrthographyBuilder()).append(lastGroup.getOrthography())
				.append($tagmarker.val).toOrthography());
	}
	|	overlap_point
	{
		Group lastGroup = 
			($u::r.numberOfGroups() > 0 ? $u::r.getGroup($u::r.numberOfGroups()-1) : $u::r.addGroup());
		lastGroup.setOrthography(
			(new OrthographyBuilder()).append(lastGroup.getOrthography())
				.append($overlap_point.val).toOrthography());
	}
	|	underline
	{
		Group lastGroup = 
			($u::r.numberOfGroups() > 0 ? $u::r.getGroup($u::r.numberOfGroups()-1) : $u::r.addGroup());
		lastGroup.setOrthography(
			(new OrthographyBuilder()).append(lastGroup.getOrthography())
				.append($underline.val).toOrthography());
	}
	|	italic
	{
		Group lastGroup = 
			($u::r.numberOfGroups() > 0 ? $u::r.getGroup($u::r.numberOfGroups()-1) : $u::r.addGroup());
		lastGroup.setOrthography(
			(new OrthographyBuilder()).append(lastGroup.getOrthography())
				.append($italic.val).toOrthography());
	}
	|	long_feature
	{
		Group lastGroup = 
			($u::r.numberOfGroups() > 0 ? $u::r.getGroup($u::r.numberOfGroups()-1) : $u::r.addGroup());
		lastGroup.setOrthography(
			(new OrthographyBuilder()).append(lastGroup.getOrthography())
				.append($long_feature.val).toOrthography());
	}
	|	nonvocal
	{
		Group lastGroup = 
			($u::r.numberOfGroups() > 0 ? $u::r.getGroup($u::r.numberOfGroups()-1) : $u::r.addGroup());
		lastGroup.setOrthography(
			(new OrthographyBuilder()).append(lastGroup.getOrthography())
				.append($nonvocal.val).toOrthography());
	}
	;
	
uendele
	:	k
	{
		TierDescription tierDesc = ensureTierExists("Markers", false);
		
		Tier<String> markersTier = $u::r.getTier(tierDesc.getName(), String.class);
		if(markersTier == null) {
			markersTier = sessionFactory.createTier(tierDesc.getName(), String.class, tierDesc.isGrouped());
			$u::r.putTier(markersTier);
		}
		
		StringBuffer buffer = new StringBuffer();
		if(markersTier.getGroup(0) != null) {
			buffer.append(markersTier.getGroup(0)).append(" ");
		}
		buffer.append($k.val);
		markersTier.setGroup(0, buffer.toString());
	}
	|	error
	{
		TierDescription tierDesc = ensureTierExists("Errors", false);
		
		Tier<String> errorsTier = $u::r.getTier(tierDesc.getName(), String.class);
		if(errorsTier == null) {
			errorsTier = sessionFactory.createTier(tierDesc.getName(), String.class, tierDesc.isGrouped());
			$u::r.putTier(errorsTier);
		}
		
		StringBuffer buffer = new StringBuffer();
		if(errorsTier.getGroup(0) != null) {
			buffer.append(errorsTier.getGroup(0)).append(" ");
		}
		buffer.append($error.val);
		errorsTier.setGroup(0, buffer.toString());
	}
	|	r
	{
		TierDescription tierDesc = ensureTierExists("Repetition", false);
		
		Tier<String> repTier = $u::r.getTier(tierDesc.getName(), String.class);
		if(repTier == null) {
			repTier = sessionFactory.createTier(tierDesc.getName(), String.class, tierDesc.isGrouped());
			$u::r.putTier(repTier);
		}
		
		StringBuffer buffer = new StringBuffer();
		if(repTier.getGroup(0) != null) {
			buffer.append(repTier.getGroup(0)).append(" ");
		}
		buffer.append($r.val);
		repTier.setGroup(0, buffer.toString());
	}
	|	a
	{
		// sub-rule a performs necessary tier management
	}
	;
]]>
    </buffer>
	
	<buffer name="AST2TalkBank.g">
		<![CDATA[
u
	:	^(U_START (attrs+=u_attrs)* (links+=linker)* (contentlist+=uele)* t (pcs+=postcode)* media? (annotationlist+=uendele)*)
	->	template(
			attrlist={$attrs},
			ucontent={$contentlist},
			linkers={$links},
			terminator={$t.st},
			postcodes={$pcs},
			segment={$media.st},
			annotations={$annotationlist},
			uttid={recordIndex++}
		)
	<<
\<u <attrlist; separator=" "> uID="u<uttid>"\>
	<linkers; separator="">
	<ucontent; separator="">
	<terminator>
	<postcodes; separator="">
	<segment>
	<annotations; separator="">
\</u\>
	>>
	;
	
u_attrs
	:	U_ATTR_WHO
	->	template(who={$U_ATTR_WHO.text})
	<<who="<who>" >>
	|	U_ATTR_LANG
	->	template(lang={$U_ATTR_LANG.text})
	<<xml:lang="<lang>" >>
	;
	
uele
	:	ugrp
	->	template( v={$ugrp.st} )
		"<v>"
	|	uannotation
	->	template( v={$uannotation.st} )
		"<v>"
	;
	
uannotation
	: 	blob
	->	template( v={$blob.st} )
		"<v>"
	|	quotation
	->	template( v={$quotation.st} )
		"<v>"
	|	quotation2
	->	template( v={$quotation2.st} )
		"<v>"
	|	pause
	->	template( v={$pause.st} )
		"<v>"
	|	internal_media
	->	template( v={$internal_media.st} )
		"<v>"
	|	freecode
	->	template( v={$freecode.st} )
		"<v>"
	|	e
	->	template( v={$e.st} )
		"<v>"
	|	s 
	->	template( v={$s.st} )
		"<v>"
	|	tagmarker
	->	template( v={$tagmarker.st} )
		"<v>"
	|	overlap_point
	->	template( v={$overlap_point.st} )
		"<v>"
	|	underline
	->	template( v={$underline.st} )
		"<v>"
	|	italic
	->	template( v={$italic.st} )
		"<v>"
	|	long_feature
	->	template( v={$long_feature.st} )
		"<v>"
	|	nonvocal
	->	template( v={$nonvocal.st} )
		"<v>"
	;
	

ugrp
	:	w 
	->	template( v={$w.st} )
		"<v>"
	|	pg
	->	template( v={$pg.st} )
		"<v>"
	|	g
	-> 	template( v={$g.st} )
		"<v>"
	|	sg
	->	template( v={$sg.st} )
		"<v>"
	;
	
uendele
	:	k
	->	template( v={$k.st} )
		"<v>"
	|	error
	->	template( v={$error.st} )
		"<v>"
	|	r
	->	template( v={$r.st} )
		"<v>"
	|	a
	->	template( v={$a.st} )
		"<v>"
	;
]]>
	</buffer>

    
    <!-- Includes -->
	<include>linker.xml</include>
    <include>w.xml</include>
	<include>blob.xml</include>
    <include>g.xml</include>
    <include>pg.xml</include>
	<include>sg.xml</include>
	<include>quotation.xml</include>
	<include>quotation2.xml</include>
    <include>pause.xml</include>
	<include>internalmedia.xml</include>
	<include>freecode.xml</include>
    <include>e.xml</include>
    <include>s.xml</include>
	<include>tagmarker.xml</include>
	<include>overlappoint.xml</include>
	<include>underline.xml</include>
	<include>italic.xml</include>
	<include>longfeature.xml</include>
	<include>nonvocal.xml</include>
    <include>t.xml</include>
	<include>postcode.xml</include>
    <include>media.xml</include>
	<include>k.xml</include>
	<include>error.xml</include>
	<include>r.xml</include>
    <include>a.xml</include>

    
</chunk>
