<?xml version="1.0" encoding="UTF-8"?>

<!-- 
Overlap
XPath: /xs:schema/xs:complexType[@name="overlapType"]
-->
<chunk xmlns="https://www.phon.ca/ns/chunk">
    
    <!-- Parser -->
    <buffer name="TalkBank2AST.g">
        <![CDATA[
overlap
	:	OVERLAP_START overlap_attr* OVERLAP_END?
	->	^(OVERLAP_START overlap_attr*)
	;	
	
overlap_attr
	:	OVERLAP_ATTR_TYPE
	|	OVERLAP_ATTR_INDEX
	;

]]>
    </buffer>
    
    <!-- Tree Walker -->
    <buffer name="AST2Phon.g">
        <![CDATA[
overlap returns [String val]
scope {
	String type;
	String index;
}
@init {
	$overlap::type = "";
	$overlap::index = "";
}
@after {
	$val = $overlap::type + $overlap::index;
}
	:	^(OVERLAP_START overlap_attr*)
	;
	
overlap_attr
	:	type=OVERLAP_ATTR_TYPE
	{
		if($type != null) {
			String ovType = $type.text;
			if(ovType.equals("overlap follows")) 
				$overlap::type = ">";
			else if(ovType.equals("overlap precedes"))
				$overlap::type = "<";
		}
	}
	|	index=OVERLAP_ATTR_INDEX
	{
		if($index != null) {
			$overlap::index = $index.text;
		}
	}
	;
]]>
    </buffer>
    
    <buffer name="AST2TalkBank.g">
        <![CDATA[
overlap
	:	^(OVERLAP_START (attrlist+=overlap_attr)*)
	->    template( attrs={$attrlist} )
	"\<overlap <attrs; separator=\" \">/\>"
	;
	
overlap_attr
	:	OVERLAP_ATTR_TYPE
	->    template( type={$OVERLAP_ATTR_TYPE.text})
	"type=\"<type>\""
	|	OVERLAP_ATTR_INDEX
	->    template( idx={$OVERLAP_ATTR_INDEX.text})
	"index=\"<idx>\""
	;
]]>
    </buffer>
    
</chunk>
