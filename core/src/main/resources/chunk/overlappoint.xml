<?xml version="1.0" encoding="UTF-8"?>

<!-- 
  overlap-point
  CA-stype overlap
-->
<chunk xmlns="https://www.phon.ca/ns/chunk">
    
    <!-- Parser -->
    <buffer name="TalkBank2AST.g">
        <![CDATA[
overlap_point
    :    OVERLAP_POINT_START overlap_pointattrs+ OVERLAP_POINT_END
    ->    ^(OVERLAP_POINT_START overlap_pointattrs+)
    ;
    
overlap_pointattrs
    :    OVERLAP_POINT_ATTR_INDEX
    |    OVERLAP_POINT_ATTR_START_END
    |    OVERLAP_POINT_ATTR_TOP_BOTTOM
    ;
]]>
    </buffer>
    
    <!-- Tree Walker -->
    <buffer name="AST2Phon.g">
        <![CDATA[
overlap_point returns [String val]
scope {
    String index;
    String startEnd;
    String topBottom;
}
@init {
    $overlap_point::index = null;
    $overlap_point::startEnd = null;
    $overlap_point::topBottom = null;
}
    :    ^(OVERLAP_POINT_START overlap_pointattrs+)
    {
        if($overlap_point::index != null) {
            $val = "(overlap-point:";
            String attrs =
                ($overlap_point::index != null ?  $overlap_point::index : "");
            $val += attrs + ")";
        } else {
            if($overlap_point::startEnd.equals("start")) {
                if($overlap_point::topBottom.equals("top")) {
                    $val = "⌈";
                } else {
                    $val = "⌊";
                }
            } else {
                if($overlap_point::topBottom.equals("top")) {
                    $val = "⌉";
                } else {
                    $val = "⌋";
                }
            }
        }
    }
    ;
    
overlap_pointattrs
    :    OVERLAP_POINT_ATTR_INDEX
    {
        $overlap_point::index = $OVERLAP_POINT_ATTR_INDEX.text;
    }
    |    OVERLAP_POINT_ATTR_START_END
    {
        $overlap_point::startEnd = $OVERLAP_POINT_ATTR_START_END.text;
    }
    |    OVERLAP_POINT_ATTR_TOP_BOTTOM
    {
        $overlap_point::topBottom = $OVERLAP_POINT_ATTR_TOP_BOTTOM.text;
    }
    ;
]]>
    </buffer>
    
    <buffer name="AST2TalkBank.g">
        <![CDATA[
overlap_point
    :    ^(OVERLAP_POINT_START (attrlist+=overlap_pointattrs)+)
    ->    template(
			attrs={$attrlist}
	)
    "\<overlap-point <attrs; separator=\" \">/\>"
    ;
    
overlap_pointattrs
    :    OVERLAP_POINT_ATTR_INDEX
    ->    template(idx={$OVERLAP_POINT_ATTR_INDEX})
    "index=\"<idx>\""
    |    OVERLAP_POINT_ATTR_START_END
    ->    template(startEnd={$OVERLAP_POINT_ATTR_START_END})
    "start-end=\"<startEnd>\""
    |    OVERLAP_POINT_ATTR_TOP_BOTTOM
    ->    template(topBottom={$OVERLAP_POINT_ATTR_TOP_BOTTOM})
    "top-bottom=\"<topBottom>\""
    ;
]]>
    </buffer>
    
</chunk>